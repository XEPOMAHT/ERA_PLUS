ERA+ mod by Херомант007
"Истощающиеся шахты" Все шахты имеют ограниченный запас ресурсов
WoGify name: script755.erm (option 755)
Author: Херомант007
Objs: 53 Шахта

!?FU30370;         ПОСТ-ИНСТРУКЦИЯ
!!UN:P755/?y1;     у1 = состояние опции
!!FU&y1=0:E;       ВЫХОД, если опция отключена
!!UN:U53/-1/?y1;   у1 = кол-во шахт
!!FU&y1<1:E;       ВЫХОД, если шахт нет
!!VRv1998:C-1/0/0; обнулить для корректной работы цикла
!!VRy2:S0;          y2 = счётчик
!!UN:X?y3/?y4;      y3 = размер карты, y4 = этажность карты
!!VRy3::36 +y2 *10; y3 = коэффициент от размера карты: S=1,L=2...G=7, +1 за подземелье *10
[:poisk_shaht]
!!UN:U53/-1/-1/1998; найти шахту
!!VRy4:Sy3 R5;       рамдомить кол-во
!!POv1998/v1999/v2000:V3/y4; запомнить кол-во
!!VRy2:+1;
!!SN&y1>y2:G[poisk_shaht];

!?FU30379;                    ЕЖЕДНЕВНЫЙ ТРИГГЕР
!!UN:P755/?y1;                у1 = состояние опции
!!FU&y1=0:E;                  ВЫХОД, если опция отключена
!!UN:U53/-1/?y1;              у1 = кол-во шахт
!!FU&y1<1:E;                  ВЫХОД, если шахт нет
!!VRv1998:C-1/0/0;            обнулить для корректной работы цикла
!!VRy2:S0;                    y2 = счётчик
[:poisk_shaht2]
!!UN:U53/-1/-1/1998;          найти шахту
!!MNv1998/v1999/v2000:O?y3/1 M6/?y5/?y6; y3 = хАзяин шахты, y5,y6 = маркер
!!POv1998/v1999/v2000&y3<>-1:V3/d-1; -1 к кол-ву, кроме безхозных
!!POv1998/v1999/v2000:V3/?y4; y4 = остаток ресурса
!!if&y4<1/y3<>148:;           шахта истощена и не маркирована?
  !!MNv1998/v1999/v2000:O-1 M6/148/1; убираем хАзяина, маркер - подвода с боеприпасами
  !!TRv1998/v1999/v2000:E1 P0; закрыть вход
  !!OBv1998/v1999/v2000:S;    запрет посещения
!!en;
!!VRy2:+1;
!!SN&y1>y2:G[poisk_shaht2];

!?FU30378;              ПРАВЫЙ КЛИК ПО КАРТЕ ПРИКЛЮЧЕНИЙ (Typhon trigger)
!!UN:P755/?y1;          у1 = состояние опции
!!FU|y1=0/v995<>53:E;   ВЫХОД, если опция отключена или клик не по шахте
!!CM:R0;
!!VRy3:Sv996*4+6976824; y3 = адрес таблицы текстов шахт
!!UN:Cy3/4/?y4;         y4 = адрес имени шахты
!!SN:Ky4/?y5;           y5 = размер строки
!!VRy5:+1;              добавить нулевой байт к размеру строки
!!SN:Ky5/y4/9597928/1;  копировать в z1
!!CM:P?y1/?y2/?y3;      y1..y3 = координаты клика
!!SN:O?y1/?y2/?y3;      у1..у3 = коорднаты входа
!!MNy1/y2/y3:R?y4/1 M6/?y5/?y6; y4 = ресурс, y5,y6 = маркер
!!if&y5<>148/y6<>1:;    шахта с маркером?
  !!POy1/y2/y3:V3/?y5;  y5 = остаток дней до закрытия
  !!VRy5|y4=0/y4=2:*2;  удвоенный запас для дерева/руды
  !!VRy5&y4=6:*1000;    *1000 для золота
  !!IF:Q1/y4/y5/4/z100301;
!!el:;
  !!IF:M0/4/z100302;
!!en:;
