ERA+ mod by Херомант007
"Драконьи города" Разграбленную Утопию Драконов можно перестроить в город за 30 дерева и 30 руды.
WoGify name: script757.erm (option 757)
Autor: Херомант007
Objs: 25 Утопия Драконов

!$OB25;
!!UN:P757/?y1;            y1 = значение опции 757
!!FU&y1=0:E;              выход, если опция не включена
!!CBv998/v999/v1000:T?y1; y1 = 1, если утопия разграблена
!!FU&y1=0:E;              выход, если утопия не разграблена
!!UN:U98/-1/?y1;          y1 = Количество городов на карте
!!FU&y1>=48:E;            выход, если на карте уже максимальное количество городов
!!HE-1:O?y1 B2/?y2;       y1 = хозяин героя, y2 = класс героя
!!VRy2::2;                y2 = тип фракции
!!OW:Ry1/0/?y3 Ry1/2/?y4; y3/y4 = кол-во дерева/руды у игрока
!!if&y3>29/y4>29:;        если есть ресурсы
  !!if&1000:;             человек
    !!VRy5:Sy2 +37;       y5 = номер картинки-форта для города родного герою
    !!IF:Q1/0/-100030/y5/7/2/-100030/2/z100300;
    !!if&1:;
      !!UN:Ov998/v999/v1000; убрать утопию
      !!VRy6:Sv998 +1;    коррекция координаты города
      !!UN:Iy6/v999/v1000/98/y2/98/y2/-1/0; поставить город
      !!CAv998/v999/v1000:Oy1 B2/30 B2/31 R1; хозяин и строения
      !!OW:Ry1/0/d-30 Ry1/2/d-30; забрать бабки
      !!HE-1:W0/1;        обездвижить
      !!UN:R1;
    !!en:;
  !!el:;                  ИИ
    !!UN:Ov998/v999/v1000; убрать утопию
    !!VRy6:Sv998 +1;      коррекция координаты города
    !!UN:Iy6/v999/v1000/98/y2/98/y2/-1/0; поставить город
    !!CAv998/v999/v1000:Oy1 B2/30 B2/31 R1; хозяин и строения
  !!en:;
!!en:;
