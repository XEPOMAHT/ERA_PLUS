ERA+ mod by Херомант007
"Кармические битвы"
WoGify name: script038.erm (option 38)
Autor: Херомант007

!?BF&1000;
!!UN:P38/?y1; у1 = состояние опции
!!FU&y1=0:E;  ВЫХОД, если опция отключена
!!BA:H1/?y1; y1 = защитник
!!FU&y1>-1:E; ВЫХОД, если не бомжи
!!BU:N1/?y1; y1 = кол-во бомжей
!!VRy2:S21; y2 = счётчик
!!VRy1:+y2;
!!VRy3:C-1/-1; y3, y4 = босс и его репа
[:find_boss]
!!BMy2:U16/?y5; y5 = репа монстра
!!if&y5>y4:;
  !!VRy3:Sy2;
  !!VRy4:Sy5;
!!en:;
!!VRy2:+1;
!!SN&y2<y1:G[find_boss];
!!BMy3:T?y25 N?y26 U10/?y27 U11/?y28; y25 = тип, y26 = кол-во, y27 = город, y28 = уровень
!!FU&y25=173:E; ВЫХОД, если босс - Санта
!!if&y28>0/y27>-1:; главарь не нейтрал и не сопляк
  !!VRy28:-1;
  !!UN:Ty27/y28/0/?y29; y29 = кармонстр
!!el:; остальные - от почвы
  !!BU:G?y24; y24 = накладная
  !!if&y24>-1:;
    !!VRy30:C214/28/56/0/42/14/197/224/84/118/197/232/224/224/224/224;
    !!VRy24:+30;
    !!VRy29:Syy24; y29 = кармонстр
  !!el:;
    !!BU:L?y24; y24 = почва
    !!VRy30:C56/232/14/28/98/84/70/42/197/224/118/232/232/232/232/232;
    !!VRy24:+30;
    !!VRy29:Syy24; y29 = кармонстр
  !!en:;
!!en:;
!!VRy1:C15/185/32/168/49/151/66/134/83/117/100;
!!VRy12:C14/184/31/167/48/150/65/133/82/116/99;
!!VRy24:S0; y24 = счётчик
[:findfreepos]
!!VRy24:+1;
!!BU:Dyy24/?y25; y25 = есть кто-то
!!SN&y24<22/y25<>-1:G[findfreepos];
!!BU:Sy29/y26/yy24/1/-1/0;
