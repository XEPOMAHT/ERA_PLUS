ERA+ mod by Херомант007
"Перестройка и Разрушение Объектов"
WoGify name: script77.erm (option 99)
Objs:2 Жертвенный Алтарь
    10 Палатка Ключника
    13 Картограф
    14 Озеро Алого Леблядя
    15 Вуаль Тьмы
    16, 24, 25, 84 Банки Монстров
    27 Око Мага
    28 Круг Фей
    30 Фонтан Удачи
    31 Источник Вечной Молодости
    33 Гарнизон
    38 Идол Удачи
    48 Волшебный Родник
    49 Волшебный Колодец
    58, 60 Наблюдательная Вышка
    63 Пирамида
    64 Знамя Великой Битвы
    78 Лагерь Беженцев
    80 Святилище

!?FU30000;                 каждый день опции скрипта Перестройки
!!UN:P99/?y1;              у1 - состояние опции
!!FU&y1=0:E;               ВЫХОД, если опция отключена
!!VRy1:S0;                 y1 - номер героя
!!UN:C4968773/4/?y2;       y2 - кол-во героев
[:fix1985]
!!IF:Wy1;                  установить героя
!!HEy1:P?w198/?w199/?w200; записать позицию в w198..w200
!!VRy1:+1;
!!SN&y1<y2:G[fix1985];

!?FU1207967744;           48002000 (пост-ЖЕРТВЕННЫЙ АЛТАРЬ)
!!UN:P99/?y1;             у1 - состояние опции
!!FU&y1=0:E;              ВЫХОД, если опция отключена
!!OBv998/v999/v1000:U?y1; у1 - подтип объекта
!!IF:W-1;                 устанавливаем текущего героя
!!VRy2:S1;                у2 - результат проверки стартовых координат
!!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
!!VRy3:S-1;               у3 - куда двигать заменяемый объект
!!VRy3&y1=1:S1;           фикс алтаря
!!FU97&y1>-1/y1<2/y2=1:Py3/8/2; если подтип корректный (0-1)

!?FU1208000512;           4800A000 (пост-ПАЛАТКА КЛЮЧНИКА)
!!UN:P99/?y1;             у1 - состояние опции
!!FU&y1=0:E;              ВЫХОД, если опция отключена
!!OBv998/v999/v1000:U?y1; у1 - подтип объекта
!!UN:U9/y1/?y2;           у2 - количество стражей границы
!!FU&y2>0:E;              ВЫХОД, если есть стражи границы
!!IF:W-1;                 устанавливаем текущего героя
!!VRy2:S1;                у2 - результат проверки стартовых координат
!!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
!!FU97&y1>-1/y1<8/y2=1:P-1/y1/10; если подтип корректный (0-7)

!?FU1208012800;           4800D000 (пост-КАРТОГРАФ)
!!UN:P99/?y1;             у1 - состояние опции
!!FU&y1=0:E;              ВЫХОД, если опция отключена
!!OBv998/v999/v1000:U?y1; у1 - подтип объекта
!!IF:W-1;                 устанавливаем текущего героя
!!VRy2:S1;                у2 - результат проверки стартовых координат
!!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
!!VRy3:S8 +y1;            у3 - номер гиф-файла
!!FU97&y1>0/y1<3/y2=1:P1/y3/13; если подтип корректный (1-2)

!?FU1208016896;           4800E000 (пост-ПРУД АЛОГО ЛЕБЕДЯ)
!!UN:P99/?y1;             у1 - состояние опции
!!FU&y1=0:E;              ВЫХОД, если опция отключена
!!OBv998/v999/v1000:U?y1; у1 - подтип объекта
!!IF:W-1;                 устанавливаем текущего героя
!!VRy2:S1;                у2 - результат проверки стартовых координат
!!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
!!HE-1:W?y3;              у3 - очки передвижения героя
!!HE-1&y3<6:Wd+6/1;       прибавить копейки для входа в функцию
!!FU97&y1=0/y2=1:P1/11/14; если подтип корректный (0)

!?FU1208020992;           4800F000 (пост-ВУАЛЬ ТЬМЫ)
!!UN:P99/?y1;             у1 - состояние опции
!!FU&y1=0:E;              ВЫХОД, если опция отключена
!!OBv998/v999/v1000:U?y1; у1 - подтип объекта
!!IF:W-1;                 устанавливаем текущего героя
!!VRy2:S1;                у2 - результат проверки стартовых координат
!!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
!!FU97&y1>-1/y1<2/y2=1:P1/12/15; если подтип корректный (0-1)

!?FU1208025088;           48010000 (пост-БАНК МОНСТРОВ)
!!OBv998/v999/v1000:T?y3; y3 - тип объекта
!!FU&y3<>16/y3<>24/y3<>25/y3<>84:E; ВЫХОД, если это не банк монстров
!!UN:P99/?y1;             у1 - состояние опции
!!CBv998/v999/v1000:T?y2; у2 - разграблен ли банк
!!FU&y1=0|y2=0:E;         ВЫХОД, если опция отключена или банк не разграблен
!!OBv998/v999/v1000:U?y1; у1 - подтип объекта
!!VRy1&y3=25:S10;         правка для утопии драконов
!!VRy1&y3=84:S9;          правка для склепа
!!if|y1=6/y1=19/y1=21/y1=22/y1=26/y1=27/y1=31/y1=35/y3=24:; эти подтипы только ломаем
  !!if&1000:;             для человека
    !!HE-1:O?y1 S4/?y2;   у1 - хозяин, у2 - дипломат
    !!OW:Ry1/6/?y3;       у3 - золото
    !!if&y2>0/y3>249;     дипломат? есть золото?
      !!IF:Q1/100045;
      !!if&1:;
        !!OW:Ry1/6/d-250; снять бабло
        !!HE-1:W0;        обнулить шаги
        !!UN:Ov998/v999/v1000/0/2; удалить объект
      !!en:;
    !!en:;
  !!en:;
!!el:;
  !!IF:W-1;               устанавливаем текущего героя
  !!VRy2:S1;              у2 - результат проверки стартовых координат
  !!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
  !!VRy3:S1;              у3 - куда двигать заменяемый объект
  !!VRy3|y1=2/y1=3/y1=10/y1=11/y1=28/y1=30/y1=32/y1=36/y1=39:S-1; фикс банка
  !!VRy4:S13 +y1;         у4 - номер гиф-файла
  !!FU97&y1>-1/y1<51/y2=1:Py3/y4/16/y1; если подтип корректный (0-50)
!!en:;

!?FU1208057856;           48018000 (пост-ПОКИНУТЫЙ КОРАБЛЬ)
!!UN:P99/?y1;             у1 - состояние опции, для безопасности
!!FU1208025088&y1<>0:P;   вызвать функцию пост-банка монстров

!?FU1208061952;           48019000 (пост-УТОПИЯ ДРАКОНОВ)
!!UN:P99/?y1;             у1 - состояние опции, для безопасности
!!FU1208025088&y1<>0:P;   вызвать функцию пост-банка монстров

!?FU1208070144;           4801B000 (пост-ОКО МАГА)
!!UN:P99/?y1;             у1 - состояние опции, для безопасности
!!FU1208082432&y1<>0:P;   вызвать функцию пост-фонтана удачи

!?FU1208074240;           4801C000 (пост-КРУГ ФЕЙ)
!!UN:P99/?y1;             у1 - состояние опции
!!FU&y1=0:E;              ВЫХОД, если опция отключена
!!OBv998/v999/v1000:U?y1; у1 - подтип объекта
!!IF:W-1;                 устанавливаем текущего героя
!!VRy2:S1;                у2 - результат проверки стартовых координат
!!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
!!FU97&y1>-1/y1<2/y2=1:P1/32/28; если подтип корректный (0-1)

!?FU1208082432;           4801E000 (пост-ФОНТАН УДАЧИ)
!!UN:P99/?y1;             у1 - состояние опции
!!FU|-1000/y1=0:E;        ВЫХОД, если ИИ или опция выключена
!!HE-1:O?y1 S4/?y2;       у1 - хозяин, у2 - дипломат
!!OW:Ry1/6/?y3;           у3 - золото
!!if&y2>0/y3>249;         дипломат? есть золото?
  !!IF:Q1/100045;
  !!if&1:;
    !!OW:Ry1/6/d-250;     снять бабло
    !!HE-1:W0;            обнулить шаги
    !!UN:Ov998/v999/v1000/0/2; удалить объект
  !!en:;
!!en:;

!?FU1208086528;           4801F000 (пост-ИСТОЧНИК ВЕЧНОЙ МОЛОДОСТИ)
!!UN:P99/?y1;             у1 - состояние опции
!!FU&y1=0:E;              ВЫХОД, если опция отключена
!!OBv998/v999/v1000:U?y1; у1 - подтип объекта
!!IF:W-1;                 устанавливаем текущего героя
!!VRy2:S1;                у2 - результат проверки стартовых координат
!!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
!!FU97&y1>-1/y1<2/y2=1:P-1/34/31; если подтип корректный (0-1)

!?FU1208094720;           48021000 (пост-ГАРНИЗОН)
!!UN:P99/?y1;             у1 - состояние опции
!!OBv998/v999/v1000:U?y2; у2 - подтип объекта
!!FU|-1000/y1=0/y2=3:E;   ВЫХОД, если ИИ или опция выключена или это не гарнизон
!!HE-1:O?y1 S4/?y2;       у1 - хозяин, у2 - дипломат
!!OW:Ry1/6/?y3;           у3 - золото
!!GRv998/v999/v1000:O?y4 G0/?y5/?y6 G1/?y7/?y8 G2/?y9/?y10 G3/?y11/?y12 G4/?y13/?y14 G5/?y15/?y16 G6/?y17/?y18;
!!if&y2>0/y3>249/y5<0/y7<0/y9<0/y11<0/y13<0/y15<0/y17<0; дипломат? есть золото? гарнизон пуст?
  !!IF:Q1/100045;
  !!if&1:;
    !!OW:Ry1/6/d-250;     снять бабло
    !!HE-1:W0;            обнулить шаги
    !!UN:Ov998/v999/v1000/0/2; удалить объект
  !!en:;
!!en:;

!?FU1208115200;           48026000 (пост-ИДОЛ УДАЧИ)
!!UN:P99/?y1;             у1 - состояние опции, для безопасности
!!FU1208082432&y1<>0:P;   вызвать функцию пост-фонтана удачи

!?FU1208156160;           48030000 (пост-ВОЛШЕБНЫЙ РОДНИК)
!!UN:P99/?y1;             у1 - состояние опции, для безопасности
!!FU1208082432&y1<>0:P;   вызвать функцию пост-фонтана удачи

!?FU1208160256;           48031000 (пост-ВОЛШЕБНЫЙ КОЛОДЕЦ)
!!UN:P99/?y1;             у1 - состояние опции, для безопасности
!!FU1208082432&y1<>0:P;   вызвать функцию пост-фонтана удачи

!?FU1208197120;           4803A000 (пост-НАБЛЮДАТЕЛЬНАЯ ВЫШКА)
!!UN:P99/?y1;             у1 - состояние опции, для безопасности
!!FU1208082432&y1<>0:P;   вызвать функцию пост-фонтана удачи

!?FU1208205312;           4803C000 (пост-ОГНЕННЫЙ СТОЛП)
!!UN:P99/?y1;             у1 - состояние опции, для безопасности
!!FU1208082432&y1<>0:P;   вызвать функцию пост-фонтана удачи

!?FU1208217600;           4803F000 (пост-ПИРАМИДА)
!!UN:P99/?y1;             у1 - состояние опции
!!FU&y1=0:E;              ВЫХОД, если опция отключена
!!PMv998/v999/v1000:V?y1; y1 - посещена ли пирамида
!!FU&y1=1:E;              ВЫХОД, если пирамида посещена
!!OBv998/v999/v1000:U?y1; у1 - подтип объекта
!!IF:W-1;                 устанавливаем текущего героя
!!VRy2:S1;                у2 - результат проверки стартовых координат
!!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
!!FU97&y1>-1/y1<3/y2=1:P-1/35/63; если подтип корректный (0-2)

!?FU1208221696;           48040000 (пост-РАЗОРВАННЫЙ ФЛАГ)
!!UN:P99/?y1;             у1 - состояние опции
!!FU&y1=0:E;              ВЫХОД, если опция отключена
!!OBv998/v999/v1000:U?y1; у1 - подтип объекта
!!IF:W-1;                 устанавливаем текущего героя
!!VRy2:S1;                у2 - результат проверки стартовых координат
!!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
!!FU97&y1=0/y2=1:P-1/39/64; если подтип корректный (0)

!?FU1208279040;           4804E000 (пост-ЛАГЕРЬ БЕЖЕНЦЕВ)
!!UN:P99/?y1;             у1 - состояние опции
!!FU&y1=0:E;              ВЫХОД, если опция отключена
!!OBv998/v999/v1000:C?y1; у1 - контр. слово
!!FU&y1<>0:E;             ВЫХОД, если лагерь не пуст
!!VRy1:S0;                y1 - лево/право от героя
!!VRy2:Sv998-1;
!!OBy2/v999/v1000:T?y3;   у3 - левый тип объекта
!!VRy1&y3=78:S-1;
!!if&y1=0:;
  !!VRy2:Sv998+1;
  !!OBy2/v999/v1000:T?y3; у3 - правый тип объекта
  !!VRy1&y3=78:S1;
!!en:;
!!IF:W-1;                 устанавливаем текущего героя
!!VRy2:S1;                у2 - результат проверки стартовых координат
!!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
!!FU97&y1<>0/y2=1:Py1/40/78;

!?FU1208287232;           48050000 (пост-СВЯТИЛИЩЕ)
!!UN:P99/?y1;             у1 - состояние опции
!!FU&y1=0:E;              ВЫХОД, если опция отключена
!!OBv998/v999/v1000:U?y1; у1 - подтип объекта
!!IF:W-1;                 устанавливаем текущего героя
!!VRy2:S1;                у2 - результат проверки стартовых координат
!!VRy2&w198=v998/w199=v999/w200=v1000:S0; фикс бага скрипта Перестройки
!!FU97&y1>-1/y1<2/y2=1:P-1/44/80; если подтип корректный (0-1)

!?FU1208303616;           48054000 (пост-СКЛЕП)
!!UN:P99/?y1;             у1 - состояние опции, для безопасности
!!FU1208025088&y1<>0:P;   вызвать функцию пост-банка монстров













!?FU97;                      ---ФУНКЦИЯ СТРОИТЕЛЬСТВА И РАЗРУШЕНИЯ ОБЪЕКТОВ КАРТЫ---
!!HE-1:O?y1 S4/?y2 W?y3;     y1 - хозяин, y2, y3 - сколько шагов осталось герою-дипломату?
!!OW:Ry1/6/?y4 Ry1/0/?y5 Ry1/2/?y6 Ry1/1/?y7 Ry1/3/?y8 Ry1/4/?y9 Ry1/5/?y10 ; у4 - золото, у5 - дерево, у6 - руда, у7 - ртуть, у8 - сера, у9 - кристалл, у10 - самоцвет
!!VRy11:Sv998+x1;            у11 - фикс положения башни
!!UN:X?y12/?y13;             у12 - размер карты, у13 - кол-во уровней
!!FU|y2<1/y3<6/y4<250/y11<0/y11>=y12:E; ВЫХОД, если не дипломат или нет шагов или нет 250 золота или на границе карты
!!if&1000:;                  для человека
  !!if&x3=16:;               если это банк монстров
    !!UN:C4695225/4/?y12;    y12 - базовый адрес имён в структуре банков
    !!VRy13:Sx4 *400 + y12;  y13 - адрес адреса имени
    !!SN:G[propuskObjNames];
  !!en:;
  !!if&x3<233:;              если это номер строки из ObjNames.txt
    !!VRy13:Sx3 *4 +6978132; y13 - адрес адреса имени
    [:propuskObjNames]
    !!UN:Cy13/4/?y14;        y14 - адрес имени
    !!SN:Ky14/?y15;          y15 - размер строки
    !!VRy15:+1;              добавить нулевой байт к размеру строки
    !!SN:Ky15/y14/9597928/1; копировать в z1
  !!el:;
    !!VRz1:Szx3;
  !!en:;
  !!IF:D99/z100040/0/z100041/^Zmap%X2.pcx^/0/0/0/z1/0/0/0/z100042/z100043/z100044/z100045 E1/99; подготовка диалога, вызов, v1 - результат
  !!if&v1=1:;                если строим монстрятник
    !!if&y4>999/y5>4/y6>4:; если есть ресурсы
      !!OW:Ry1/0/d-5 Ry1/2/d-5 Ry1/6/d-1000; снять бабло
      !!HE-1:W0 B2/?y12;     обнулить шаги, у12 - класс героя
      !!VRy12::2 +101;       у12 - делить класс пополам
      !!UN:Ov998/v999/v1000/0/0 Iy11/v999/v1000/17/y12/17/y12/-1/1; удалить объект, добавить жилище
      !!DWy11/v999/v1000:M0/d/0; обнулить жителей в монстрятнике
    !!el:;
      !!IF:Q1/0/10/2/10/6/5000/1/z100046; если мало - посылаем найух
    !!en:;
  !!en:;
  !!if&v1=2:;                если строим мельницу
    !!if&y5>9/y9>1/y10>1:;  если есть ресурсы
      !!OW:Ry1/0/d-10 Ry1/4/d-2 Ry1/5/d-2; снять бабло
      !!HE-1:W0;             обнулить шаги
      !!VRy11:+1;            у11 - фикс положения мельницы
      !!TRy11/v999/v1000:T?y16; y16 - тип почвы
      !!UN:Ov998/v999/v1000/0/0 Iy11/v999/v1000/112/0/112/0/y16/1; удалить объект, добавить мельницу
    !!el:;
      !!IF:Q1/0/20/4/5/5/5/1/z100046; если мало - посылаем найух
    !!en:;
  !!en:;
  !!if&v1=3:;                если строим дозорную башню
    !!if&y6>9/y7>1/y8>1:;   если есть ресурсы
      !!OW:Ry1/2/d-10 Ry1/1/d-2 Ry1/3/d-2; снять бабло
      !!HE-1:W0;             обнулить шаги
      !!TRy11/v999/v1000:T?y16; y16 - тип почвы
      !!UN:Ov998/v999/v1000/0/0 Iy11/v999/v1000/33/2/33/2/y16/1; удалить объект, добавить дозорную башню
    !!el:;
      !!IF:Q1/2/20/1/5/3/5/1/z100046; если мало - посылаем найух
    !!en:;
  !!en:;
  !!if&v1=4:;                 если чистим местность
    !!OW:Ry1/6/d-250;         снять бабло
    !!HE-1:W0;                обнулить шаги
    !!UN:Ov998/v999/v1000/0/2; удалить объект
  !!en:;
!!el:;                       ИИ
  !!HE-1:W0 B2/?y12;         обнуляем шаги, у12 - класс героя
  !!VRy12::2 +101;           у12 - делим класс пополам
  !!UN:Ov998/v999/v1000/0/0 Iy11/v999/v1000/17/y12/17/y12/-1/1; удалить объект, добавить жилище
  !!DWy11/v999/v1000:Oy1;    хозяин - ИИ
!!en:;
