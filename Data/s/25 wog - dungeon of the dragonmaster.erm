ERA+ mod by Херомант007
"Темница Хозяина Драконов"
WoGify name: script053.erm (option 53)
Autor: Херомант007
Objs: 25 Утопия Драконов

!?FU30370;        ПОСТ-ИНСТРУКЦИЯ
!!UN:P53/?y1; у1 = состояние опции
!!FU&y1=0:E;  ВЫХОД, если опция отключена
!!UN:U25/0/?y1; у1 = кол-во утопий
!!FU&y1<1:E;  ВЫХОД, если утопий нет
!!VRv1998:C-1/0/0; обнулить для корректной работы цикла
!!VRy2:S0;    y2 = счётчик
[:poisk_utopii]
!!UN:U25/0/-1/1998; найти утопию
!!CBv1998/v1999/v2000:G4/151/1 G5/155/1 G6/134/1;
!!POv1998/v1999/v2000:N0; обнулить флаг награды
!!VRy2:+1;
!!SN&y1>y2:G[poisk_utopii];

!?OB25;
!!UN:P53/?y1; у1 = состояние опции
!!FU&y1=0:E; ВЫХОД, если опция отключена
!!MA:O134/1; для нулевой морали у драконов

!$OB25;
!!UN:P53/?y1; у1 = состояние опции
!!FU&y1=0:E; ВЫХОД, если опция отключена
!!MA:O134/2; восстановить
!!CBv998/v999/v1000:T?y1;
!!FU&y1=0:E; ВЫХОД, если утопия не разграблена
!!POv998/v999/v1000:N?y1;
!!FU&y1=1:E; ВЫХОД, если хозяин утопии ушёл
!!HE-1:O?y1;
!!FU&y1<0:E; ВЫХОД, если герой мертв
!!if&1000:; человек
  !!HE-1:B0/?z1; z1 = имя героя
  !!IF:Q1/21/65670/17/5000/7/z100207; спросить
  !!HE-1&1:C2/134/1/1; дать дракона
  !!HE-1&-1:Ed+5000; дать опыт
!!el:; ИИ
  !!HE-1:C2/134/1/0; дать дракона
!!en:;
!!POv998/v999/v1000:N1;
