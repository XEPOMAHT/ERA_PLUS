ERA+
** Накладные ERM-почвы by Херомант007
** WoGify name: script71.erm (option 95)
** Objs: 1(1) Растрескавшиеся Льды
**       2(18) Дюны
**       3(19) Поля Славы
**       4(40) Излияния Магмы

!?FU30352;    ПЕРЕД БИТВОЙ
!!OBv998/v999/v1000:T?y2 U?y3;
!!HE-1&y2=34:R8/?y2; если герой - получить тип, в котором он стоит
!!VRy3:+1;
!!if&y2=16:;  если банк монстров
  !!BA&y3>13/y3<18:By3; если банки с эмиссарами - сменить фон
!!en:;
!!BA&y2=25:B^dragbattle.pcx^; если утопия драконов - сменить фон

!?FU30800;    НАСТРОЙКА ПОЛЯ БОЯ
!!UN:P95/?y1; у1 - состояние опции
!!FU&y1=0:E;  ВЫХОД, если опция отключена
!!BU:G?y1;    у1 - тип накладной почвы
!!if&y1=10:;  если растрескавшиеся льды
  !!VRy1:S0;  у1 - номер стека
  [:стеки10]; перебираем все стеки
  !!BMy1:D0;  обнулить защиту
  !!VRy1:+1;
  !!SN&y1<42:G[стеки10];
  !!FU:E;     выход нах
!!en:;
!!if&y1=13:;   если излияния магмы
  !!VRy1:S0;   у1 - номер стека
  [:стеки20];  перебираем все стеки
  !!BMy1:Ad*2; удвоить атаку
  !!VRy1:+1;
  !!SN&y1<42:G[стеки20];
  !!FU:E;    выход нах
!!en:;
!!if&y1=11:;  если дюны
  !!VRy1:S178;
  [:quicksand];
  !!BM0:Q0/y1/0;
  !!VRy1:-17;
  !!SN&y1>0:G[quicksand];
!!en:;

!?FU77006;    ПОСЛЕ ПЕРЕДАЧИ КОНТРОЛЯ
!!UN:P95/?y1; у1 - состояние опции
!!FU&y1=0:E;  ВЫХОД, если опция отключена
!!BU:G?y1;    у1 - тип накладной почвы
!!if&y1=12:;  если поле славы
  !!VRy1:S0;  у1 - номер стека
  [:стеки12]; перебираем все стеки
  !!BMy1:G213/d-2/d G212/d-2/d;
  !!VRy1:+1;
  !!SN&y1<42:G[стеки12];
!!en:;

!?FU30304;    ПОСЛЕ ДЕЙСТВИЯ
!!UN:P95/?y1; у1 - состояние опции
!!FU&y1=0:E;  ВЫХОД, если опция отключена
!!BU:G?y1;    у1 - тип накладной почвы
!!if&y1=12:;  если поле славы
  !!VRy1:S0;  у1 - номер стека
  [:стеки12]; перебираем все стеки
  !!BMy1:G213/d-2/d G212/d-2/d;
  !!VRy1:+1;
  !!SN&y1<42:G[стеки12];
!!en:;
