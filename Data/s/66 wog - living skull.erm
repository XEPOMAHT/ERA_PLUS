ERA+ mod by Херомант007
"Живой Череп"
WoGify name: script012.erm (option 12)
Autor: Херомант007
Objs: 166 Живой череп

!?OB166/-1&1000; не для ИИ
!!HE-1:B0/?z1 O?y1; z1 = имя героя, y1 = хазяин
!!IF:Q1/8/300/8/301/10/z100106; спросить, что надо искать
!!if&v1>0:;
  !!VRv10:C1/2/4/8/16/32/64/128; массив для конверта хазяина в битовый флаг
  !!VRv1998:C-1/0/0; поиск с несуществующих координат
  !!VRv2:S0; v2 - кол-во видимых объектов
  !!VRy3:Sy1 +10; y3 = номер v-переменной для конверсии хозяина во флаг
  !!if&v1=1:; артефакты
    !!UN:U5/-1/?y2; y2 = кол-во артов
    !!DO96/1/y2/1&y2>0:P5/-1/vy3; поиск видимых игроку артов
    !!if&v2>0:; если незримые арты имеются
      !!VRy4:Sv2 *100; y4 = 100 бабок за каждый невидимый арт
      !!IF:Q1/36/y4/2/z100107; готов ли игрок забашлять?
      !!if&1:; откроем арты, снимем бабло
        !!OW:Ry1/6/?y5; y5 = бабло
        !!if&y5<y4:;
          !!VRy6:Sy4 -y5; y6 = разница бабла
          !!IF:Q1/36/y6/1/z100108;
          !!FU:E;
        !!en:;
        !!OW:Ry1/6/d-y4; снять бабло
        !!VRv1998:C-1/0/0; поиск с несуществующих координат
        !!DO96/1/y2/1:P5/-1/vy3/666; открыть все квадраты
      !!en:;
    !!el:;
      !!IF:M0/1/z100109; нет артов - нечего искать
      !!FU:E;
    !!en:;
  !!el:; ресурсы
    !!UN:U79/-1/?y2; y2 = кол-во ресурсов
    !!DO96/1/y2/1&y2>0:P79/-1/vy3; поиск видимых игроку ресурсов
    !!if&v2>0:; если незримые ресурсы имеются
      !!VRy4:Sv2 *10; y4 = 10 бабок за каждый невидимый ресурс
      !!IF:Q1/36/y4/2/z100110; готов ли игрок забашлять?
      !!if&1:; откроем ресурсы, снимем бабло
        !!OW:Ry1/6/?y5; y5 = бабло
        !!if&y5<y4:;
          !!VRy6:Sy4 -y5; y6 = разница бабла
          !!IF:Q1/36/y6/1/z100108;
          !!FU:E;
        !!en:;
        !!OW:Ry1/6/d-y4; снять бабло
        !!VRv1998:C-1/0/0; поиск с несуществующих координат
        !!DO96/1/y2/1:P79/-1/vy3/666; открыть все квадраты
      !!en:;
    !!el:;
      !!IF:M0/1/z100111; нет ресурсов - нечего искать
      !!FU:E;
    !!en:;
  !!en:;
!!en:;

!?FU96;                     ФУНКЦИЯ ПОСЧЁТА ВИДИМЫХ ОБЪЕКТОВ КАРТЫ ИЛИ ИХ ОТКРЫТИЯ
!!UN:Ux1/x2/-1/1998;        найти координаты объекта
!!TRv1998/v1999/v2000:V?y1; видимость квадрата
!!VRy1:&x3;                 проверка на видимость(если y1=x3, то видим, y1=0, то не видим)
!!if&y1=0:;
  !!if&x4<>666:;
    !!VRv2:+1;
  !!el:;
    !!TRv1998/v1999/v2000:V?y1;
    !!VRy1:+x3;
    !!TRv1998/v1999/v2000:Vy1;
  !!en:;
!!en:;
