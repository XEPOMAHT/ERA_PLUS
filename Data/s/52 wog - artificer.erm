ERA+ mod by Херомант007
"Ремесленник"
WoGify name: script026.erm (option 26)
Autor: Херомант007
Objs: 172 Ремесленник

!?OB172/-1&1000; не для ИИ
!!HE-1:B0/?z1 W?y1/1; z1 = имя героя y1 = очки передвижения
!!if&y1=0:;
  !!IF:Mz100193;
  !!FU:E; ВЫХОД, если ремесленик устал
!!en:;
!!IF:Q1/8/300/2/z100190;
!!if&1:;
[:rukzak]
  !!HE-1:A14/?y1/?y2; открыть рюкзак y1 = слот, y2 = арт
  !!if&y2>6/y2<100:; арт в слоте есть
    !!VRy7:C8/9/10/11/177/-1/14/15/16/17/178/-1/20/21/22/23; апгрейды 7..22 артов
    !!VRy23:C179/-1/26/27/28/29/180/-1/32/33/34/35/36/-1/38/39; апгрейды 23..38 артов
    !!VRy39:C40/-1/42/43/44/-1/108/181/181/181/183/183/183/-1/-1/55; апгрейды 39..54 артов
    !!VRy55:C56/-1/58/59/-1/61/62/-1/64/65/-1/67/68/-1/99/98; апгрейды 55..70 артов
    !!VRy71:C-1/-1/74/75/-1/77/78/-1/-1/-1/-1/-1/126/-1/-1/-1; апгрейды 71..86 артов
    !!VRy87:C-1/-1/-1/72/-1/-1/-1/96/96/189/99/-1/188; апгрейды 86..99 артов
    !!VRy3:Syy2; апгрейд
[:remeslen]
    !!if&y3=-1:;
      !!IF:Q1/8/y2/1/z100192; нельзя апгрейдить
    !!el:;
      !!UN:Ay3/1/?y4 N0/z2/y2 N0/z3/y3; y4 = цена, z2, z3 = имена артов
      !!IF:Q1/8/y2/36/y4/8/y3/2/z100191;
      !!if&1:;
        !!OW:R-1/6/?y5; y5 = бабло
        !!if&y4<=y5:;
          !!OW:R-1/6/d-y4; снять бабло
          !!HE-1:A10/y2/y1 A9/y3/y1 W0; удалить и добавить арт из слота
        !!el:;
          !!IF:Q1/6/y4/1/z100194; нет денег - нет апгрейда
        !!en:;
        !!FU:E; ВЫХОД после апгрейда
      !!en:;
    !!en:;
  !!el&y2>174/y2<187:; арт в слоте от ЭРА+
    !!VRy4:Sy2-100;
    !!VRy75:C7/13/162/157/-1/-1/182/-1/184/-1/186/187; апгрейды 175..186 артов
    !!VRy3:Syy4; апгрейд
    !!SN:G[remeslen];
  !!el&y2<>-1:;
    !!IF:Q1/8/y2/1/z100192; нельзя апгрейдить
  !!en:;
  !!SN&y2<>-1:G[rukzak]; кликнули по арту - обратно в рюкзак
!!en:;
