ERA+ mod by Херомант007
"Дворец мечтаний"
WoGify name: script011.erm (option 11)
Autor: Херомант007
Objs: 165 Дворец мечтаний

!?OB165/-1&1000; не для ИИ
!!HE-1:B0/?z1; z1 = имя героя
!!IF:Q1/8/300/8/301/10/z100101; спросить, на что будем играть
!!if&v1>0:;
  !!VRz2:Sz100104; z2 = пустая строка
  !!if&v1=1:; выбрали артефакты
    [:casino1]
    !!OW:R-1/6/?y2; y2 = бабло
    !!if&y2<100:;
      !!IF:Q1/36/100/1/z100102;
      !!FU:E; выход, если мало бабла
    !!en:;
    !!OW:R-1/6/d-100; снять бабло
    !!VRy7:S1 R100; y7 = 1..100
    !!UN:C6919480/4/?y3; y3 = геймменеджер
    !!SN:E5018000/2/y3/2; Call SoD_GetRandArt
    !!VRy4:Sv1;
    !!if&y7<5:; шанс 5%
      !!UN:Ay4/9/2; костыли для бога костылей
      !!IF:Q1/8/y4/8/y4/8/y4/2/z100105; выйгрыш
      !!UN:Ay4/9/0; восстановить имя арта
      !!HE-1:A4/y4; дать приз
    !!el:;
      !!SN:E5018000/2/y3/2; Call SoD_GetRandArt
      !!VRy5:Sv1;
      !!SN:E5018000/2/y3/2; Call SoD_GetRandArt
      !!VRy6:Sv1;
      !!UN:Ay4/9/2 Ay5/9/2 Ay6/9/2; костыли для бога костылей
      !!IF:Q1/8/y4/8/y5/8/y6/2/z100103; проигрыш
      !!UN:Ay4/9/0 Ay5/9/0 Ay6/9/0; восстановить имена артов
    !!en:;
    !!SN&1:G[casino1];
  !!el:; выбрали ресурс
    [:casino2]
    !!OW:R-1/6/?y2; y2 = бабло
    !!if&y2<100:;
      !!IF:Q1/36/100/1/z100102;
      !!FU:E; выход, если мало бабла
    !!en:;
    !!OW:R-1/6/d-100; снять бабло
    !!VRy3:S0 R5; y3 = 0..5
    !!VRy4:S0 R5; y4 = 0..5
    !!VRy5:S0 R5; y5 = 0..5
    !!if&y3=y4/y3=y5:;
      !!IF:Q1/y3/0/y3/0/y3/0/2/z100105; выйгрыш
      !!OW:R-1/y3/d+1; дать приз
    !!el:;
      !!IF:Q1/y3/0/y4/0/y5/0/2/z100103; проигрыш
    !!en:;
    !!SN&1:G[casino2];
  !!en:;
!!en:;
