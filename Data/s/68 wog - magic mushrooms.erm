ERA+ mod by Херомант007
"Хрыбочъкы"
WoGify name: script010.erm (option 110)
Autor: Херомант007
Objs: 178 Магические Грибы

!?OB178/0&1000; не для ИИ гриб огня
!!HE-1:B0/?z1 S14/?y1; z1 = имя героя, y1 = уровень магии огня
!!if&y1>2:;
  !!IF:Mz100292;
  !!FU:E;
!!en:;
!!IF:W-1 Q1/20/47/1/z100293;
!!VRw176:Sy1 +1; т.к. 0 - ничего не делать
!!HE-1:S14/3; установить эксперта

!?OB178/1&1000; не для ИИ гриб воздуха
!!HE-1:B0/?z1 S15/?y1; z1 = имя героя, y1 = уровень магии воздуха
!!if&y1>2:;
  !!IF:Mz100294;
  !!FU:E;
!!en:;
!!IF:W-1 Q1/20/50/1/z100295;
!!VRw177:Sy1 +1; т.к. 0 - ничего не делать
!!HE-1:S15/3; установить эксперта

!?OB178/2&1000; не для ИИ гриб воды
!!HE-1:B0/?z1 S16/?y1; z1 = имя героя, y1 = уровень магии воды
!!if&y1>2:;
  !!IF:Mz100296;
  !!FU:E;
!!en:;
!!IF:W-1 Q1/20/53/1/z100297;
!!VRw178:Sy1 +1; т.к. 0 - ничего не делать
!!HE-1:S16/3; установить эксперта

!?OB178/3&1000; не для ИИ гриб земли
!!HE-1:B0/?z1 S17/?y1; z1 = имя героя, y1 = уровень магии земли
!!if&y1>2:;
  !!IF:Mz100298;
  !!FU:E;
!!en:;
!!IF:W-1 Q1/20/56/1/z100299;
!!VRw179:Sy1 +1; т.к. 0 - ничего не делать
!!HE-1:S17/3; установить эксперта

!?FU30379&v991=1; НОВЫЙ 1 ДЕНЬ
!!VRy1:S0;        y1 - номер героя
!!UN:C4960750/4/?y2; y2 - кол-во героев
[:posk_her_musrums]
!!IF:Wy1;         указать w-переменные
!!if&w176>0:;
  !!VRw176:-1;
  !!HEy1:S14/w176; восстановить магию огня
  !!VRw176:S0;
!!en:;
!!if&w177>0:;
  !!VRw177:-1;
  !!HEy1:S15/w177; восстановить магию воздуха
  !!VRw177:S0;
!!en:;
!!if&w178>0:;
  !!VRw178:-1;
  !!HEy1:S16/w178; восстановить магию воды
  !!VRw178:S0;
!!en:;
!!if&w179>0:;
  !!VRw179:-1;
  !!HEy1:S17/w179; восстановить магию земли
  !!VRw179:S0;
!!en:;
!!VRy1:+1;
!!SN&y1<y2:G[posk_her_musrums];
