ERA+
** Сборные ERM-артефакты by Херомант007
** WoGify name: null (option 281)
** Objects: art 161 - шлем королевы гидр -> круговая атака
**          art 162 - меч берсеркера -> берсерк после удара
**          art 163 - щит вельзевула -> безответность
**          art 164 - кольцо марафонца -> максимальная скорость
**          art 166 - кулон отражения -> снижение магурона на 50%
**          art 167 - наряд лесного лорда -> расстановка препятствий
**          art 168 - мантия миротворца -> -50% к агрессии монстров
**          art 170 - золотой гусь -> +2750 золотых в день

!?FU30800;               НАСТРОЙКА ПОЛЯ БОЯ
!!UN:P281/?y1;           у1 - состояние опции
!!FU&y1=0:E;             ВЫХОД, если опция отключена
!!VRy1:S0;               y1 - номер стека
[:стеки];                перебираем все стеки
!!BMy1:I?y2 B?y3;        у2 - принадлежность стека, у3 - кол-во монстров
!!BA:Hy2/?y4;            y4 - номер героя
!!if&y3>0/y4>-1:;        если есть хоть 1 монстр и есть герой
  !!HEy4:A2/161/?y5/?y6; у5 - количество копий артефакта, у6 - надет ли артефакт
  !!if&y5>0/y6=1:;       если есть ШЛЕМ КОРОЛЕВЫ ГИДР
    !!BMy1:F?y7;         y7 - флаг
    !!VRy7:&8;           есть ли дыхание дракона?
    !!if&y7=0:;
      !!BMy1:F?y7;       y7 - флаг
      !!VRy7:|524288;    добавляем атаку всех рядом стоящих
      !!BMy1:Fy7;
    !!en:;
  !!en:;
  !!HEy4:A2/163/?y5/?y6; у5 - количество копий артефакта, у6 - надет ли артефакт
  !!if&y5>0/y6=1:;       если есть ЩИТ ВЕЛЬЗЕВУЛА
    !!BMy1:F?y7;
    !!VRy7:|65536;       добавляем безответность
    !!BMy1:Fy7;
  !!en:;
  !!HEy4:A2/164/?y5/?y6; у5 - количество копий артефакта, у6 - надет ли артефакт
  !!if&y5>0/y6=1:;       если есть КОЛЬЦО МАРАФОНЦА
    !!BMy1:S?y7;         у7 - скорость стека
    !!BMy1&y7<19:S19;    установить макс. скорость
  !!en:;
  !!HEy4:A2/167/?y5/?y6; у5 - количество копий артефакта, у6 - надет ли артефакт
  !!if&y5>0/y6=1:;       если есть НАРЯД ЛЕСНОГО ЛОРДА
    !!BMy1:F?y7 P?y8 T?y9; у7 - флаг, у8 - позиция, у9 - тип стека
    !!VRy7:&64;          y7 - боевая машина?
    !!if&y7=0/y8<>86/y9<>173:; если не боевая машина, не 86-я позиция, не Санта-Гремлин
      !!BMy1:F?y7;       y7 - флаг
      !!VRy7:&1;         y7 - 2-клеточный?
      !!if&y2=0:;        если нападающий
        !!VRy8:+1;
        !!VRy8&y7=1:+1;  если 2-клеточный
      !!el:;
        !!VRy8:-1;
        !!VRy8&y7=1:-1;  если 2-клеточный
      !!en:;
      !!BF:O34/y8;       поместить корягу
    !!en:;
  !!en:;
!!en:;
!!VRy1:+1;
!!SN&y1<42:G[стеки];

!?FU30303;   ПЕРЕД ДЕЙСТВИЕМ
!!BG:A?v995; v995 - тип дейсвия в бою

!?FU77007;                       ПЕРЕД ПЕРЕДАЧЕЙ КОНТРОЛЯ
!!UN:P281/?y1;                   у1 - состояние опции
!!FU&y1=0:E;                     ВЫХОД, если опция отключена
!!BG:H?y1 N?y2 E?y3;             у1 - герой-хозяин, у2 - ходящий стек, у3 - цель
!!FU&y1<0:E;                     ВЫХОД, если нет героя
!!HEy1:A2/162/?y4/?y5;           у4 - количество копий арта, у5 - надет ли арт
!!if&v995=6/y3>-1/y4>0/y5=1:;    если атакует, есть цель и МЕЧ БЕРСЕРКЕРА
  !!BMy2:N?y6;                   у6 - кол-во в ходящем стеке
  !!BMy3:N?y7 P?y8;              у7 - кол-во в целевом стеке, у8 - его позиция
  !!BMy3&y6>0/y7>0:C59/y8/2/2/1; если оба живы - наложить берсек
!!en:;

!?FU30307;             ПЕРЕД ПОДСЧЁТОМ СОПРОТИВЛЕНИЯ
!!UN:P281/?y1;         у1 - состояние опции
!!FU&y1=0:E;           ВЫХОД, если опция отключена
!!BG:Q?y1;             у1 - ходящая сторона
!!BA&y1=1:H0/?y2;
!!BA&y1=0:H1/?y2;      y2 - номер героя противной стороны
!!FU&y2<0:E;           ВЫХОД, если нет героя
!!HEy2:A2/166/?y3/?y4; у3 - количество копий артефакта, у4 - надет ли артефакт
!!if&y3>0/y4=1:;       если есть КУЛОН ОТРАЖЕНИЯ
  !!MR:D?y5;           у5 - урон от заклинания
  !!VRy5::2;           разделить на 2
  !!MR:Dy5;            назначить новый урон
!!en:;

!?FU1073963008;        40036000 (МОНСТР)
!!UN:P281/?y1;         у1 - состояние опции
!!FU&y1=0:E;           ВЫХОД, если опция отключена
!!MO998:R?y1/1;        y1 - уровень агрессии
!!HE-1:A2/168/?y3/?y4; у3 - количество копий артефакта, у4 - надет ли артефакт
!!if&y1>1/y1<10/y3>0/y4=1:; если есть МАНТИЯ МИРОТВОРЦА
  !!VRy1::2;
  !!MO998:R1/1;
!!en:;

!?FU30379;           НОВЫЙ ДЕНЬ
!!UN:P281/?y1;       у1 - состояние опции
!!FU&y1=0:E;         ВЫХОД, если опция отключена
!!VRy1:S0;           y1 - номер героя
!!UN:C4960750/4/?y2; y2 - кол-во героев
[:perebor_her]
!!HEy1:O?y3;         у3 - хозяин
!!if&y3>-1:;         если есть хозяин
  !!HEy1:A2/170/?y4/?y5; , у4 - кол-во артов, у5 - надет ли арт
  !!OW&y4>0/y5=1:Ry3/6/d5000;  если есть ЗОЛОТОЙ ГУСЬ - дать золото
!!en:;
!!VRy1:+1;
!!SN&y1<y2:G[perebor_her];
