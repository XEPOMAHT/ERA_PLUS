ERA+ mod by Херомант007
"Расширенная неделя монстров"
WoGify name: script020.erm (option 20, 134, 135, 136, 200)
Autor: Херомант007

!?CM0;
!!CM:I?y1 S?y2; y1 = тип клика, y2 = подтип клика
!!if&y1=12/y2=14:;
  !!UN:C41916832/4/?y3 C41916836/4/?y4 C41916824/4/?y5 C41916828/4/?y6; y3/y4 = тип/подтип месяца, y5/y6 = ... недели
  !!FU&y3=0/y5=0:E; ВЫХОД, если неделя/месяц неизвестны
  !!if&y3=0:; месяц ничего
    !!VRy4:+100150; 0->100150
    !!VRz1:Szy4; текст месяца
    !!if&y5=0:; неделя ничего
      !!VRy6:+100160; 0->100160
      !!VRz2:Szy6; текст недели
      !!IF:M0/4/^%Z1%Z2^;
    !!en:;
    !!if&y5=1:; неделя монстра
      !!UN:N3/2/y6/0 N3/3/y6/1; z2,z3 = имя монстра
      !!IF:Q1/21/y6/4/z100144;
    !!en:;
    !!IF&y5=2:Q1/21/42/21/43/4/z100145; неделя беса
  !!en:;
  !!if&y3=1:; месяц монстра
    !!UN:N3/2/y4/0 N3/3/y4/1; z2,z3 = имя монстра
    !!if&y5=0:; неделя ничего
      !!VRy6:+100160; 0->100160
      !!VRz1:Szy6; текст недели
      !!IF:Q1/21/y4/4/z100146;
    !!en:;
    !!if&y5=1:; неделя монстра
      !!UN:N3/4/y6/0 N3/5/y6/1; z4,z5 = имя монстра
      !!IF:Q1/21/y4/21/y6/4/z100147;
    !!en:;
    !!IF&y5=2:Q1/21/42/21/43/4/z100145; неделя беса
  !!en:;
  !!IF&y3=2:M0/4/z100148; месяц чумы
  !!CM:R0;
!!en:;

!?FU30800;    НАСТРОЙКА ПОЛЯ БОЯ
!!UN:P20/?y1; у1 = состояние опции
!!FU&y1=0:E;  ВЫХОД, если опция отключена
!!UN:C41916832/4/?y2 C41916836/4/?y3 C41916824/4/?y4 C41916828/4/?y5; y2/y3 = тип/подтип месяца, y4/y5 = ... недели
!!FU&y2<>1/y4<>1:E; ВЫХОД, если не монстры в неделе
!!VRy1:S-1;    y1 = номер стека
!!VRy3&y2<>1:S6666;
!!VRy5&y4<>1:S6666;
[:отряды];
!!VRy1:+1;
!!BMy1:T?y6;  y6 = тип монстра
!!SN&y1=20:G[отряды]; пропуск стрелк.башни
!!BMy1&y3=y6:Ad+1 Dd+1; +1 к атаке и защите
!!BMy1&y5=y6:Sd+1; +1 к скорости
!!SN&y1<40:G[отряды];

!?FU30379&v991=1;  НОВЫЙ 1 ДЕНЬ
!!VRy2:S0 R5;      y2 = случайный ресурс
!!UN:P134/?y1;     у1 = состояние опции
!!SN&y1=0:G[o135]; ПРЫЖОК, если опция отключена
!!UN:X?y3/?y4;     y4 = уровневость
!!VRy5:Sy3 -1;     y5 = размер карты
!!VRy3::18;        y3 = кол-во циклов
!!VRy6:Sy3 *5;
!!IF:Q1/y2/y6/1/z100149;
!!VRy6:S0;         y6 = счётчик
!!VRy18:S0;        y18 = счётчик безопасности
!!UN:C7410821/1/-1; да воде в UN_E
[:o134A];
!!VRy18:+1;
!!SN&y18>100:G[o134B]; ПРЫЖОК, если цикл завис
!!VRy7:S0 Ry5;     y7 = х
!!VRy8:S0 Ry5;     y8 = y
!!VRy9:S0;
!!VRy9&y4=1:R1;    y9 = l
!!UN:Ey7/y8/y9;
!!SN&1:G[o134A];
!!TRy7/y8/y9:T?y10/?y11/?y12/?y13/?y14/?y15/?y16/?y17; y10 = тип почвы
!!if&y10=8:; на воде
  !!if&y2=0:; дерево
    !!UN:Iy7/y8/y9/29/0/29/0/-1/0;
    !!OBy7/y8/y9:C0;
  !!en:;
  !!if&y2=2:; камни
    !!UN:Iy7/y8/y9/29/2/29/2/-1/0;
    !!OBy7/y8/y9:C0;
  !!en:;
  !!if|y2=1/y2>2:; остальные
    !!UN:Iy7/y8/y9/29/1/29/1/-1/0;
    !!VRy11:Sy2 -1;
    !!VRy11&y2>1:-1;
    !!VRy11:+512;
    !!OBy7/y8/y9:Cy11;
  !!en:;
!!el:;
  !!UN:Iy7/y8/y9/12/1/12/1/-1/0;
  !!FRy7/y8/y9:By2/5;
!!en:;
!!VRy6:+1;
!!SN&y6<y3:G[o134A];
[:o134B];
!!UN:C7410821/1/8; нет воде в UN_E
[:o135]
!!UN:P135/?y1;     у1 = состояние опции
!!SN&y1=0:G[o136]; ПРЫЖОК, если опция отключена
!!UN:C41916824/4/?y10 C41916828/4/?y11; y10/y11 = тип/подтип недели
!!SN&y10<>1:G[o136]; ПРЫЖОК, если не неделя монстра
!!UN:X?y3/?y4;     y4 = уровневость
!!VRy5:Sy3 -1;     y5 = размер карты
!!VRy3::36;        y3 = кол-во циклов
!!VRy6:S0;         y6 = счётчик
!!VRy14:S0;        y14 = счётчик безопасности
[:o135A];
!!VRy14:+1;
!!SN&y14>100:G[o136]; ПРЫЖОК, если цикл завис
!!VRy7:S0 Ry5;     y7 = х
!!VRy8:S0 Ry5;     y8 = y
!!VRy9:S0;
!!VRy9&y4=1:R1;    y9 = l
!!UN:Ey7/y8/y9;
!!SN&1:G[o135A];
!!UN:Iy7/y8/y9/54/y11/54/y11/-1/0;
!!MA:Vy11/?y12;    y12 = мин.кол-во
!!VRy12::2+1;      снизить в 2 раза
!!MOy7/y8/y9:Gy12;
!!VRy6:+1;
!!SN&y6<y3:G[o135A];
[:o136]
!!UN:P136/?y1;     у1 = состояние опции
!!SN&y1=0:G[o200]; ПРЫЖОК, если опция отключена
!!UN:U112/0/?y3;   у3 = кол-во мельниц
!!SN&y3<1:G[o200]; ПРЫЖОК, если мельниц нет
!!VRv1998:C-1/0/0; стартовые координаты
!!VRy4:S0;         y4 = счётчик
[:o136A];
!!UN:U112/0/-1/1998;
!!MLv1998/v1999/v2000:By2/d;
!!VRy4:+1;
!!SN&y4<y3:G[o136A];
[:o200]
!!UN:P200/?y1;     у1 = состояние опции
!!FU&y1=0:E;       ВЫХОД, если опция отключена
!!UN:U78/-1/?y3;   у3 = кол-во лагерей
!!FU&y3<1:E;       ВЫХОД, если лагерей нет
!!UN:C41916832/4/?y5 C41916836/4/?y6 C41916824/4/?y7 C41916828/4/?y8; y5/y6 = тип/подтип месяца, y7/y8 = ... недели
!!VRy9:S-1;        y9 = монстр
!!VRy9&y5=1:Sy6;
!!VRy9&y7=1:Sy8;
!!FU&y9=-1:E;      ВЫХОД, если монстров нет
!!MA:Ly9/?y10;     y10 = уровень
!!FU&y10>5:E;      ВЫХОД, если уровень 7
!!VRv1998:C-1/0/0; стартовые координаты
!!VRy4:S0;         y4 = счётчик
[:o200A];
!!UN:U78/-1/-1/1998;
!!OBv1998/v1999/v2000:Uy9;
!!VRy4:+1;
!!SN&y4<y3:G[o200A];
