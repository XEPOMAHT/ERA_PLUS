ERA+ mod by Херомант007
"Расширенный Шпионаж"
WoGify name: script73.erm (option 96)
Objs:2 Жертвенный Алтарь
     4 Арена
     5 Артефакт
     6 Ящик Пандоры
     7 Чёрный Рынок
     8 Корабль
    11 Буй
    12 Бивачий Костёр
    13 Картограф
    14 Пруд Алого Леблядя
    15 Вуаль Тьмы
    16,24,25 Банки Монстров
    17,20 Жилища Существ
    22 Труп
    23 Башня Марлетто
    27 Око Мага
    28 Круг Фей
    29 Обломки
    30 Фонтан Удачи
    31 Источник Вечной Молодости
    32 Сад Откровения
    35 Форт на Холме
    37 Хижина Мага
    38 Идол Удачи
    39 Навес
    41 Библиотека Просвещения
    42 Маяк
    43..45 Порталы
    47 Школа Магии
    48 Волшебный Ручей
    49 Волщебный Колодец
    50 Колизей Магов
    51 Лагерь Наёмников
    52 Русалочья Скала
    53 Шахта
    55 Мистический Сад
    56 Оазис
    58 Наблюдательная Вышка
    60 Огненный Столп
    61 Планетарий
    62 Тюрьма

!?FU30378;            ПРАВЫЙ КЛИК ПО КАРТЕ ПРИКЛЮЧЕНИЙ (Typhon trigger)
!!UN:P96/?y1;         у1 - состояние опции
!!FU&y1=0:E;          ВЫХОД, если опция отключена
!!OW:A-1/?y1;         y1 - номер активного героя
!!if&y1>-1:;          если герой выбран
  !!HEy1:R12/?y2;     y2 - уровень шпионажа
  !!if&y2>2:;         если герой-шпион
    !!VRy3:Sv995;
    !!if&v995=29;     имена для тифоновских объектов
      !!VRy3&v996=1:S17;
      !!VRy3&v996=2:S20;
    !!en:;
    !!VRy3&v995=55/v996=2:S16;
    !!VRy3:*4+6978132; y3 - адрес таблицы имён объектов
    !!UN:Cy3/4/?y4;   y4 - адрес имени объекта
    !!SN:Ky4/?y5;     y5 - размер строки
    !!VRy5:+1;        добавить нулевой байт к размеру строки
    !!SN:Ky5/y4/9597928/1; копировать в z1
    !!if&v995=2:;     ЖЕРТВЕННЫЙ АЛТАРЬ
      !!CM:R0;
      !!IF:Q1/100/0/4/z100050;
      !!FU:E;
    !!en:;
    !!if&v995=4:;       АРЕНА
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!OBy3/y4/y5:C?y6; у6 - контр.слово
      !!HEy1:V7/y6/?y7; у7 - флаг посещёности арены
      !!IF&y7=0:Q1/31/2/32/2/4/z100051; не посещено
      !!IF&y7=1:Q1/100/1/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=5:;          АРТЕФАКТ
      !!CM:R0;
      !!UN:C6687592/4/?y3; y3 - адрес таблицы текстов артефактов
      !!VRy4:Sv996 *32 +y3 +16; y4 - адрес адреса строки описания артефакта
      !!UN:Cy4/4/?y5;      y5 - адрес строки описания артефакта
      !!SN:Ky5/?y6;        y6 - размер строки
      !!VRy6:+1;           добавить нулевой байт к размеру строки
      !!SN:Ky6/y5/9597928/1; копировать в z1
      !!IF:Q1/8/v996/4/z1;
      !!FU:E;
    !!en:;
    !!if&v995=6:;     ЯЩИК ПАНДОРЫ
      !!CM:R0;
      !!IF:Q1/100/2/4/z100053;
      !!FU:E;
    !!en:;
    !!if&v995=7:;     ЧЁРНЫЙ РЫНОК
      !!CM:R0;
      !!IF:Q1/100/3/4/z100054;
      !!FU:E;
    !!en:;
    !!if&v995=8:;     КОРАБЛЬ
      !!CM:R0;
      !!IF:Q1/100/4/4/z100055;
      !!FU:E;
    !!en:;
    !!if&v995=11:;    БУЙ
      !!CM:R0;
      !!HEy1:R7/?y3;  у3 - флаг посещённых объектов
      !!VRy3:&4;      у3 - посещён ли буй
      !!IF&y3=0:Q1/14/1/4/z100056; не посещено
      !!IF&y3=4:Q1/100/5/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=12:;    БИВАЧИЙ КОСТЁР
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!FRy3/y4/y5:B?y6/?y7; у6 - тип ресурса, у7 - кол-во
      !!VRy8:Sy7 *100;
      !!IF:Q1/6/y8/y6/y7/4/z100057;
      !!FU:E;
    !!en:;
    !!if&v995=13:;    КАРТОГРАФ
      !!CM:R0;
      !!VRy3:Sv996 +6; y3 - подтип картинки
      !!VRy4:Sv996 +100058; y4 - номер строки
      !!IF:Q1/100/y3/4/zy4;
      !!FU:E;
    !!en:;
    !!if&v995=14:;    ПРУД АЛОГО ЛЕБЕДЯ
      !!CM:R0;
      !!HEy1:R7/?y3;  у3 - флаг посещённых объектов
      !!VRy3:&8;      у3 - посещён ли пруд
      !!IF&y3=0:Q1/11/2/4/z100061; не посещено
      !!IF&y3=8:Q1/100/9/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=15:;    ВУАЛЬ ТЬМЫ
      !!CM:R0;
      !!IF:Q1/100/10/4/z100062;
      !!FU:E;
    !!en:;
    !!if|v995=16/v995=24/v995=25:; БАНК МОНСТРОВ
      !!CM:R0;
      !!UN:C4695225/4/?y3;  у3 - адрес таблицы адресов имён
      !!VRv996&v995=24:S8;  v995 - подтип покинутого корабля
      !!VRv996&v995=25:S10; v995 - подтип утопии драконов
      !!VRy4:Sv996*400+y3;  у4 - адрес адреса строки
      !!UN:Cy4/4/?y5;       у5 - адрес строки
      !!SN:Ky5/?y6;         у6 - размер строки
      !!VRy6:+1;            добавить нулевой байт к размеру строки
      !!SN:Ky6/y5/9597928/1; копировать в z1
      !!CM:P?y3/?y4/?y5;    у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5;    у3, у4, у5 - коорднаты входа
      !!CBy3/y4/y5:T?y6;    у6 - разграблен ли банк
      !!if&y6=0;            если банк не разграблен
        !!VRy8:S-1;         у8 - счётчик
        !!VRy9:S10;         y9 - номер переменной, начиная с y10
        [:banki];
        !!VRy8:+1;
        !!CBy3/y4/y5:Gy8/?y6/?y7; у5 - охранник, у7 - кол-во
        !!if&y6>-1/y7>0:;   если охранник есть
          !!VRyy9:Sy6+2;    сохранить его номер спрайта в y10..y16
          !!VRy19:Sy9+10;
          !!VRyy19:Sy7;     сохранить количество в y20...y26
          !!VRy9:+1;
        !!en:;
        !!SN&y8<6:G[banki]; если охранник есть - продолжть цикл
        !!if&y9>10/y9<18:;  если охранники нашлись
          !!DL99:N^MoPDlg0099.txt^; загрузить диалог в память
          !!DL99:A8/3/z1;   записать в диалог имя жилища
          !!VRy8:S10;       y8 - счётчик
          [:bankdialog]
          !!DL99:Ay8/4/yy8; менять элементы диалога 10..16
          !!VRy18:Sy8+10;
          !!VRz1:M3/yy18;   inttostr из y20..26 в z1
          !!DL99:Ay18/3/z1; менять элементы диалога 20..26
          !!VRy8:+1;
          !!SN&y8<y9:G[bankdialog];
          !!CM:A?y8/?y9;    координаты клика
          !!FU99:P99/y8/y9; показать диалог MoP99
        !!el:;              если банк не разграблен, а охрана мертва
          !!IF:M0/4/z100064;
        !!en:;
      !!el:;                если банк разграблен
        !!IF:M0/4/z100065;
      !!en:;
      !!FU:E;
    !!en:;
    !!if|v995=17/v995=20:; ЖИЛИЩЕ МОНСТРОВ
      !!CM:R0;
      !!UN&v995=17:C4240673/4/?y3; у3 - адрес таблицы адресов имён
      !!UN&v995=20:C4240729/4/?y3; у3 - адрес таблицы адресов имён
      !!VRy4:Sv996*4+y3;   у4 - адрес адреса строки
      !!UN:Cy4/4/?y5;      у5 - адрес строки
      !!SN:Ky5/?y6;        у6 - размер строки
      !!VRy6:+1;           добавить нулевой байт к размеру строки
      !!SN:Ky6/y5/9597928/1; копировать в z1
      !!CM:P?y3/?y4/?y5;   у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5;   у3, у4, у5 - коорднаты входа
      !!DWy3/y4/y5:O?y6;   y6 - хозяин жилища
      !!HEy1:O?y7;         y7 - хозяин героя
      !!if&y6<>y7:;        если игрок - не хозяин
        !!VRy8:S-1;        y8 - счётчик
        !!VRy9:S10;        y9 - номер переменной, начиная с y10
        [:dwellingi]
        !!VRy8:+1;
        !!DWy3/y4/y5:Gy8/?y6/?y7; у6 - охранник, у7 - кол-во
        !!if&y6>-1/y7>0:;  если охранник есть
          !!VRyy9:Sy6+2;   сохранить его номер спрайта в y10..y16
          !!VRy19:Sy9+10;
          !!VRyy19:Sy7;    сохранить количество в y20...y26
          !!VRy9:+1;
        !!en:;
        !!SN&y8<6:G[dwellingi]; проверить все слоты охраны
        !!if&y9>10/y9<18:;  если охранники нашлись
          !!DL99:N^MoPDlg0099.txt^; загрузить диалог в память
          !!DL99:A8/3/z1;   записать в диалог имя жилища
          !!VRy8:S10;       y8 - счётчик
          [:dwdialog]
          !!DL99:Ay8/4/yy8; менять элементы диалога 10..16
          !!VRy18:Sy8+10;
          !!VRz1:M3/yy18;   inttostr из y20..26 в z1
          !!DL99:Ay18/3/z1; менять элементы диалога 20..26
          !!VRy8:+1;
          !!SN&y8<y9:G[dwdialog];
          !!CM:A?y8/?y9;    координаты клика
          !!FU99:P99/y8/y9; показать диалог MoP99
        !!el:;              если охраны нет
          !!SN:G[bezohrani];
        !!en:;
      !!el:;                если игрок - хозяин
        !!if:;              пустая проверка для прыжка
          [:bezohrani]
          !!VRy8:S-1;       y8 - счётчик
          !!VRy9:S10;       y9 - номер переменной, начиная с y10
          [:dwellingj]
          !!VRy8:+1;
          !!DWy3/y4/y5:My8/?y6/?y7; у6 - охранник, у7 - кол-во
          !!if&y6>-1/y7>0:; если жилец есть
            !!VRyy9:Sy6;    сохранить его в y10..y16
            !!VRy19:Sy9+10;
            !!VRyy19:Sy7;   сохранить количество в y20...y26
            !!VRy9:+1;
          !!en:;
          !!SN&y8<3:G[dwellingj];  проверить все слоты охраны
          !!if&y9>10/y9<15:;       если жильцы нашлись
            !!DL98:N^MoPDlg00%Y9.txt^; загрузить диалог в память
            !!OW:C?y4;             y4 - цвет текущего игрока
            !!DL98:A101/13/y4;     записать цвет
            !!DL98:A1000/3/z1;     записать имя жилища
            !!UN:C4697553/4/?y30;  y30 - адрес структуры монстра
            !!VRy31:S9;            y31 - счётчик
            !!VRy32:Sy9-1;         y32 - до куда считать
            [:dwdlgedit]
            !!VRy31:+1;
            !!VRyy31:*116+y30;     y10 - структура монстра
            !!VRy4:Syy31+24;       y4 - адрес имени монстра
            !!UN:Cy4/4/?y5;        у5 - адрес строки
            !!SN:Ky5/?y6;          у6 - размер строки
            !!VRy6:+1;             добавить нулевой байт к размеру строки
            !!SN:Ky6/y5/9597928/1; копировать в z1
            !!VRy33:S291+y31;      y33 - элемент диалога
            !!DL98:Ay33/3/z1;      записать имя монстра
            !!VRy4:Syy31+12;       y4 - адрес боевого дефа
            !!UN:Cy4/4/?y5;        у5 - адрес строки
            !!SN:Ky5/?y6;          у6 - размер строки
            !!VRy6:+1;             добавить нулевой байт к размеру строки
            !!SN:Ky6/y5/9597928/1; копировать в z1
            !!VRy33:S6656+y31;     y33 - элемент диалога
            !!DL98:Ay33/9/z1;      записать боевой деф
            !!UN:Cyy31/4/?y3;      y3 - фракция
            !!VRy3:*4;
            !!UN:C4494928/4/?y4;   y4 - адрес таблицы фонов фракций
            !!VRy4:+y3;
            !!UN:Cy4/4/?y5;        у5 - адрес строки
            !!SN:Ky5/?y6;          у6 - размер строки
            !!VRy6:+1;             добавить нулевой байт к размеру строки
            !!SN:Ky6/y5/9597928/1; копировать в z1
            !!VRy33:S191+y31;      y33 - элемент диалога
            !!DL98:Ay33/11/z1;     записать фон фракции
            !!VRy33:S-2+y31;       y33 - элемент диалога
            !!VRy34:Sy31+10;
            !!VRy35:Syy34;         y35 - кол-во монстров
            !!DL98:Ay33/3/z100048; записать количество в жилище
            !!VRy4:Syy31+4;        y4 - адрес уровня монстра
            !!UN:Cy4/4/?y5;        у5 - уровень монстра
            !!if&y5=0:;            монстры 1 уровня - за бесплатно
              !!VRy33:S6+y31;
              !!VRy34:S10+y31;
              !!DL98:Ay33/9/^mopsky.def^ Ay34/9/^mopsky.def^;
              !!SN:G[besplatno];
            !!en:;
            !!VRy8:S28;            y8 - счётчик
            [:osobyires]
            !!VRy8:+4;
            !!VRy4:Syy31+y8;       y4 - адрес ресурса монстра
            !!UN:Cy4/4/?y5;        у5 - кол-во ресурса
            !!SN&y5=0/y8<56:G[osobyires]; проверить все слоты ресурсов
            !!VRy9:S2+y31;         y9 - позиция золота на диалоге
            !!if&y8<56:;           ресурс найден
              !!VRy8:-32:4;        y8 - номер спрайта ресурса
              !!VRy9:S10+y31;      y9 - позиция золота на диалоге
              !!VRy33:S6+y31;      y33 - элемент диалога
              !!DL98:Ay33/4/y8;    установить спрайт ресурса
              !!VRy5:*y20;         помножить на кол-во монстров
              !!VRz1:M3/y5;        inttostr из y5 в z1
              !!VRy33:S18+y31;     y33 - элемент диалога
              !!DL98:A28/3/z1;     записать количество
              !!VRy33:S2+y31;      y33 - элемент диалога
              !!DL98:A12/9/^mopsky.def^;
            !!el:;                 не найден
              !!VRy33:S6+y31;
              !!VRy34:S10+y31;
              !!DL98:Ay33/9/^mopsky.def^ Ay34/9/^mopsky.def^;
            !!en:;
            !!VRy4:Syy31+56;       y4 - адрес кол-ва золота
            !!UN:Cy4/4/?y5;        у5 - кол-во золота
            !!if&y5>0:;            если чего-то стоит
              !!DL98:Ay9/4/6;      установить спрайт золота
              !!VRy34:Sy31+10;
              !!VRy35:Syy34;       y35 - кол-во монстров
              !!VRy5:*y35;         помножить на кол-во монстров
              !!VRz1:M3/y5;        inttostr из y5 в z1
              !!VRy9:+12;
              !!DL98:Ay9/3/z1;     записать количество
            !!el:;                 не найден
              [:besplatno]
              !!VRy33:S2+y31;
              !!VRy34:S14+y31;
              !!DL98:Ay33/9/^mopsky.def^ Ay34/3/z100049;
            !!en:;
            !!SN&y31<y32:G[dwdlgedit];
            !!CM:A?y8/?y9;         координаты клика
            !!FU99:P98/y8/y9;      показать диалог MoP98
          !!el:;
            !!IF:M0/4/z100063;
          !!en:;
        !!en:;
      !!en:;
      !!FU:E;
    !!en:;
    !!if&v995=22:;       ТРУП
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!SKy3/y4/y5:S?y6; у6 - флаг наличия арта
      !!if&y6>0:;        если арт есть
        !!SKy3/y4/y5:A?y7; у7 - номер арта
        !!IF:Q1/8/y7/4/z100066;
      !!el:;
        !!IF:Q1/100/11/4/z100065;
      !!en:;
      !!FU:E;
    !!en:;
    !!if&v995=23:;       БАШНЯ МАРЛЕТТО
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!OBy3/y4/y5:C?y6; у6 - контр.слово
      !!HEy1:V1/y6/?y7;  у7 - флаг посещёности башни марлетто
      !!IF&y7=0:Q1/32/1/4/z100067; не посещено
      !!IF&y7=1:Q1/100/12/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=27:;    ОКО МАГА
      !!CM:R0;
      !!IF:Q1/100/13/4/z100068;
      !!FU:E;
    !!en:;
    !!if&v995=28:;    КРУГ ФЕЙ
      !!CM:R0;
      !!HEy1:R7/?y3;  у3 - флаг посещённых объектов
      !!VRy3:&8192;   у3 - посещён ли круг фей
      !!IF&y3=0:Q1/11/1/4/z100069; не посещено
      !!IF&y3=8192:Q1/100/14/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=29:;       ТИФОНОВСКИЕ ВОДНЫЕ ОБЪЕКТЫ
      !!UN:Cv997/1/?y3;  y3 - первый байт контрольного слова
      !!if|v996=0/v996=2:; ОБЛОМКИ | ПОТЕРЯННЫЙ ГРУЗ
        !!CM:R0;
        !!if&y3<4:;      если что-то есть
          !!if&y3<2:;    если есть золото
            !!VRy3:+1*5; y3 - кол-во ресурса
            !!VRy4:Sy3*50; y4 - кол-во золота
            !!IF:Q1/6/y4/v996/y3/4/z100070;
          !!el:;         если только ресурс
            !!VRy3:-1*5; y3 - кол-во ресурса
            !!IF:Q1/v996/y3/4/z100070;
          !!en:;
        !!el:;           если ничего нет
          !!IF:M0/4/z100071;
        !!en:;
        !!FU:E;
      !!en:;
      !!if&v996=1:;      МОРСКАЯ БОЧКА
        !!CM:R0;
        !!if&y3<4:;      если что-то есть
          !!VRy4:Sv997+1;
          !!UN:Cy4/1/?y5; y5 - второй байт конрольного слова
          !!VRy5:+3;      y5 - кол-во ресурса
          !!VRy3:+1;
          !!VRy3&y3>1:+1; y3 - тип ресурса
          !!IF:Q1/y3/y5/4/z100070;
        !!el:;            если ничего нет
          !!IF:M0/4/z100071;
        !!en:;
        !!FU:E;
      !!en:;
    !!en:;
    !!if&v995=30:;       ФОНТАН УДАЧИ
      !!CM:R0;
      !!HEy1:R7/?y3;     у3 - флаг посещённых объектов
      !!VRy3:&939524128; у3 - посещён ли фонтан удачи
      !!IF&y3=0:Q1/13/1/11/1/4/z100072; не посещено
      !!IF&y3>0:Q1/100/15/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=31:;   ИСТОЧНИК ВЕЧНОЙ МОЛОДОСТИ
      !!CM:R0;
      !!HEy1:R7/?y3; у3 - флаг посещённых объектов
      !!VRy3:&16384; у3 - посещён ли фонтан удачи
      !!IF&y3=0:Q1/14/1/4/z100073; не посещено
      !!IF&y3=16384:Q1/100/16/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=32:;       САД ОТКРОВЕНИЯ
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!OBy3/y4/y5:C?y6; у6 - контр.слово
      !!HEy1:V2/y6/?y7;  у7 - флаг посещёности сада откровения
      !!IF&y7=0:Q1/34/1/4/z100074; не посещено
      !!IF&y7=1:Q1/100/17/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=35:;       ФОРТ НА ХОЛМЕ
      !!CM:R0;
      !!IF:Q1/100/18/4/z100075;
      !!FU:E;
    !!en:;
    !!if&v995=37:;       ХИЖИНА МАГА
      !!CM:R0;
      !!IF:Q1/100/19/4/z100076;
      !!FU:E;
    !!en:;
    !!if&v995=38:;       ИДОЛ УДАЧИ
      !!CM:R0;
      !!IF:Q1/100/20/4/z100077;
      !!FU:E;
    !!en:;
    !!if&v995=39:;       НАВЕС
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!LNy3/y4/y5:B?y6/?y7; y6 - тип ресурса, y7 - кол-во
      !!if&y7>0:;        если ресурс есть
        !!IF:Q1/y6/y7/4/z100078;
      !!el:;
        !!IF:Q1/100/21/4/z100065;
      !!en:;
      !!FU:E;
    !!en:;
    !!if&v995=41:;       БИБЛИОТЕКА ПРОСВЕЩЕНИЯ
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!OBy3/y4/y5:C?y6; у6 - контр.слово
      !!HEy1:V6/y6/?y7;  у7 - флаг посещёности арены
      !!IF&y7=0:N31/2/32/2/33/2/34/2 N4/z100079; не посещено
      !!IF&y7=1:Q1/100/22/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=42:;       МАЯК
      !!CM:R0;
      !!IF:Q1/100/23/4/z100080;
      !!FU:E;
    !!en:;
    !!if&v995>42/v995<46:; ПОРТАЛЫ
      !!CM:R0;
      !!VRy3:Sv995+100038;
      !!IF:Q1/100/24/4/zy3;
      !!FU:E;
    !!en:;
    !!if&v995=47:;       ШКОЛА МАГИИ
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!OBy3/y4/y5:C?y6; у6 - контр.слово
      !!HEy1:V8/y6/?y7;  у7 - флаг посещёности школы магии
      !!IF&y7=0:Q1/33/1/34/1/4/z100084; не посещено
      !!IF&y7=1:Q1/100/25/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=48:;       ВОЛШЕБНЫЙ РУЧЕЙ
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!SPy3/y4/y5:S?y6; y6 - есть ли что-то в источнике
      !!HEy1:F?y7/?y8/?y9/?y10; y10 - знания
      !!VRy10:*10;       y10 - макс очки маны
      !!IF&y6=1:Q1/35/y10/4/z100085; не посещено
      !!IF&y6=0:Q1/100/26/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=49:;   ВОЛШЕБНЫЙ КОЛОДЕЦ
      !!CM:R0;
      !!HEy1:R7/?y3; у3 - флаг посещённых объектов
      !!VRy3:&1;     у3 - посещён ли волшебный колодец
      !!if&y3=0:;    не посещено
        !!HEy1:F?y4/?y5/?y6/?y7 I?y8/1; y7 - знания, y8 - мана
        !!VRy7:*10-y8;
        !!IF:Q1/35/y7/4/z100086;
      !!el:;
        !!IF:Q1/100/27/4/z100052; посещено
      !!en:;
      !!FU:E;
    !!en:;
    !!if&v995=50:;       КОЛИЗЕЙ МАГОВ
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!OBy3/y4/y5:C?y6; у6 - контр.слово
      !!HEy1:V10/y6/?y7; у7 - флаг посещёности колизея магов
      !!IF&y7=0:Q1/33/2/34/2/4/z100087; не посещено
      !!IF&y7=1:Q1/100/28/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=51:;       ЛАГЕРЬ НАЁМНИКОВ
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!OBy3/y4/y5:C?y6; у6 - контр.слово
      !!HEy1:V3/y6/?y7;  у7 - флаг посещёности лагеря наёмников
      !!IF&y7=0:Q1/31/1/4/z100088; не посещено
      !!IF&y7=1:Q1/100/29/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=52:;   РУСАЛОЧЬЯ СКАЛА
      !!CM:R0;
      !!HEy1:R7/?y3; у3 - флаг посещённых объектов
      !!VRy3:&32768; у3 - посещена ли русалочья скала
      !!IF&y3=0:Q1/11/1/4/z100089; не посещено
      !!IF&y3=32768:Q1/100/30/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=53:;       ШАХТА
      !!CM:R0;
      !!VRy3:Sv996*4+6976824; y3 - адрес таблицы текстов шахт
      !!UN:Cy3/4/?y4;    y4 - адрес имени шахты
      !!SN:Ky4/?y5;      y5 - размер строки
      !!VRy5:+1;         добавить нулевой байт к размеру строки
      !!SN:Ky5/y4/9597928/1; копировать в z1
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!MNy3/y4/y5:R?y6/1 O?y7/1; y6 - ресурс, y7 - хозяин
      !!VRy8:S1;
      !!VRy8|y6=0/y6=2:S2;
      !!VRy8&y6=6:S1000;
      !!IF&y7>-1:Q1/y6/y8/10/y7/4/z100090;
      !!IF&y7=-1:Q1/y6/y8/4/z100090;
      !!FU:E;
    !!en:;
    !!if&v995=55:;       МИСТИЧЕСКИЙ САД
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!GDy3/y4/y5:T?y6 B?y7; y6 - есть ли бонус, y7 - ресурс
      !!VRy8:S5;
      !!VRy8&y7=6:S500;
      !!IF&y6=1:Q1/y7/y8/4/z100091; не посещено
      !!IF&y6=0:Q1/100/31/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=56:;   ОАЗИС
      !!CM:R0;
      !!HEy1:R7/?y3; у3 - флаг посещённых объектов
      !!VRy3:&128;   у3 - посещён ли оазис
      !!IF&y3=0:Q1/14/1/4/z100092; не посещено
      !!IF&y3=128:Q1/100/32/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=58:;   НАБЛЮДАТЕЛЬНАЯ ВЫШКА
      !!CM:R0;
      !!IF:Q1/100/33/4/z100093;
      !!FU:E;
    !!en:;
    !!if&v995=60:;   ОГНЕННЫЙ СТОЛП
      !!CM:R0;
      !!IF:Q1/100/34/4/z100094;
      !!FU:E;
    !!en:;
    !!if&v995=61:;       ПЛАНЕТАРИЙ
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!OBy3/y4/y5:C?y6; у6 - контр.слово
      !!HEy1:V4/y6/?y7;  у7 - флаг посещёности планетария
      !!IF&y7=0:Q1/33/1/4/z100095; не посещено
      !!IF&y7=1:Q1/100/35/4/z100052; посещено
      !!FU:E;
    !!en:;
    !!if&v995=62:;       ТЮРЬМА
      !!CM:R0;
      !!CM:P?y3/?y4/?y5; у3, у4, у5 - координаты клика по карте
      !!SN:O?y3/?y4/?y5; у3, у4, у5 - коорднаты входа
      !!OBy3/y4/y5:C?y6; у6 - контр.слово
      !!HEy6:R13/0;      показать мини-окно героя
    !!en:;







  !!en:;
!!en:;

!?FU99;                ---ПОКАЗАТЬ ВОГ-ДИАЛОГ ПО ПКМ И ВЫГРУЗИТЬ ЕГО ПОСЛЕ ПОКАЗА---
!!SN:E7510739/1/x1;    v1 - адрес загруженного диалога
!!VRy1:Sv1;            y1 - сохранить адрес, т.к. v1 будет затёрт эровскими вызовами
!!if&y1<>0:;           если диалог загружен
  !!UN:Cy1/4/?y2;      y2 - адрес главного элемента диалога
  !!VRy3:Sy2+32;       y3 - адрес размера Х диалога
  !!UN:Cy3/4/?y4;      y4 - размер Х диалога
  !!VRy5:Sy4:2;        y5 - центр диалога по Х
  !!UN:C4199496/4/?y6; y6 - размер Х окна игры
  !!if&x2<>-1:;        если не по центру экрана - по середине клика мыши
    !!VRy7:Sy6-y4-208; y7 - макс Х диалога
    !!VRx2:-y5;
    !!VRx2&x2>y7:Sy7;  макс = размер Х окна приключений + тень
    !!VRx2&x2<8:S8;    мин = 8
  !!el:;               если нет - по центру экрана
    !!VRx2:Sy6:2-y5-104;
  !!en:;
  !!VRy3:Sy2+24;       y3 - адрес Х
  !!UN:Cy3/4/x2;       записать Х
  !!VRy3:Sy2+36;       y3 - адрес размера Y диалога
  !!UN:Cy3/4/?y4;      y4 - размер Y диалога
  !!VRy5:Sy4:2;        y5 - центр диалога по Y
  !!UN:C4199503/4/?y6; y6 - размер Y экрана игры
  !!if&x3<>-1:;        если не по центру экрана - по середине клика мыши
    !!VRy7:Sy6-y4-55;  y7 - макс Y диалога
    !!VRx3:-y5;
    !!VRx3&x3>y7:Sy7;  макс = размер Y окна приключений + тень
    !!VRx3&x3<8:S8;    мин = 8
  !!el:;               если нет - по центру экрана
    !!VRx3:Sy6:2-y5;
  !!en:;
  !!VRy3:Sy2+28;       y3 - адрес Y
  !!UN:Cy3/4/x3;       записать Y
  !!SN:E6245264/2/y2;  показать главный элемент диалога как диалог типа 4
  !!SN:E7518240/2/y1;  выгрузить диалог после показа
!!el:;
  !!IF:M^Your fucking dialog not worked!!!^;
!!en:;
